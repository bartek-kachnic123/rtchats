plugins {
	id 'java'
	id 'pmd'
	id 'org.springframework.boot' version '3.4.7'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.diffplug.spotless' version '7.0.4'
	id 'com.github.spotbugs' version '6.2.1'
}

group = 'com.kachnic'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	googleJavaFormatVersion = '1.17.0'
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

spotless {
ratchetFrom 'origin/main'

format 'misc', {
	target '*.gradle', '.gitattributes', '.gitignore'

	trimTrailingWhitespace()
	leadingSpacesToTabs()
	endWithNewline()
}
java {
	googleJavaFormat(googleJavaFormatVersion).aosp().reflowLongStrings().skipJavadocFormatting()
	formatAnnotations()
}
}

pmd {
	toolVersion = "7.15.0"
	consoleOutput = true
	incrementalAnalysis = true
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.named('pmdMain') {
	group = 'verification'
}

tasks.named('pmdTest') {
	group = 'verification'
}
