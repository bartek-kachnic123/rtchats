plugins {
	id 'com.kachnic.java-common-conventions'
	id 'com.kachnic.format-common-conventions'
	id 'com.kachnic.format-java-conventions'
	id 'com.kachnic.format-misc-conventions'
	id 'pmd'
	id 'com.github.spotbugs'
}

pmd {
	toolVersion = libs.versions.pmd.get()
	consoleOutput = true
	incrementalAnalysis = true
	ruleSetConfig = resources.text.fromUri(this.class.getResource('pmd/ruleset.xml'))
	ruleSets = []
}

tasks.named('pmdMain') {
	group = 'verification'
}

tasks.named('pmdTest') {
	group = 'verification'
}

tasks.register('lintAndFormat') {
	group = 'verification'
	description = 'Run PMD and SpotBugs analysis, then apply Spotless formatting'

	dependsOn 'pmdMain', 'pmdTest', 'spotbugsMain', 'spotbugsTest'
	finalizedBy 'spotlessApply'
}
